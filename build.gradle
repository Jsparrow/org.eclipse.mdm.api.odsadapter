apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

repositories {
    mavenLocal()
}

sourceSets {
    generated {
        java {
            srcDir 'src/gen/java'
        }
    }
    
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
}

dependencies {
    compile 'org.eclipse.mdm:org.eclipse.mdm.api.base:1.0.0-SNAPSHOT'
//    generatedCompile 'org.jacorb:jacorb-idl-compiler:2.3.1'
    generatedCompile files(System.getenv('JAVA_HOME') + '/lib/tools.jar')
}

task compileIDL(type: JavaExec) {
    classpath = configurations.generatedCompile
//    main = 'org.jacorb.idl.parser'
//    args '-d', 'src/gen/java/', 'src/main/idl/ods530.idl'
   
   /*
    * NOTE: The CORBA IDL compiler from the JDK generates union structs 
    * that are different to those generated by the JacORB IDL compiler!
    */
   
    main = 'com.sun.tools.corba.se.idl.toJavaPortable.Compile'
    args '-fallTIE', '-td', 'src/gen/java/', 'src/main/idl/ods530.idl'
}

// generate classes from idl and compile them
compileGeneratedJava.dependsOn compileIDL
compileJava.dependsOn compileGeneratedJava

jar {
    from sourceSets.generated.output
    dependsOn generatedClasses
}

task deleteGenerated(type: Delete) {
    delete 'src/gen/'
}

clean.dependsOn deleteGenerated